plugins {
    id("org.jetbrains.kotlin.jvm") version("1.4.0")
    id("org.jetbrains.kotlin.plugin.serialization") version("1.4.0")
    id("java")
    id("com.github.johnrengelman.shadow") version("5.2.0")
}

group "me.shedaniel"
sourceCompatibility = targetCompatibility = 1.8

configurations {
    shadow
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://maven.fabricmc.net" }
    maven { url "https://jitpack.io" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx/" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-dev/" }
    maven { url "https://dl.bintray.com/shedaniel/linkie" }
}

dependencies {
    depend "me.shedaniel:linkie-core:1.0.39"
    depend "com.discord4j:discord4j-core:3.1.0", true
    depend "org.graalvm.js:js-scriptengine:20.2.0"
    depend "org.graalvm.js:js:20.2.0"
    depend "io.ktor:ktor-server-core:$ktor_version"
    depend "io.ktor:ktor-server-netty:$ktor_version"
//        exclude(module: "truffle-api")
//    compile("org.graalvm.sdk:graal-sdk:20.2.0")
//    compile("org.graalvm.truffle:truffle-api:20.2.0")
}

def depend(str, forced = false) {
    dependencies {
        compile(str) {
            force = forced
        }
        shadow(str) {
            force = forced
        }
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'me.shedaniel.linkie.discord.LinkieBot'
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier null
}

compileKotlin {
    kotlinOptions.suppressWarnings = true
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn", "-Xinline-classes"]
    }
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean
